
natural_newline = _{"\r\n" | "\n"}
explicit_newline = _{";"}
new_line = _{natural_newline | explicit_newline}
line_terminator = _{new_line | EOI}


identifier = @{ "_"* ~ ASCII_ALPHANUMERIC ~ (ASCII_ALPHANUMERIC | "_" | ".")* }
string = @{ "\"" ~ (("\\\"" | !"\"")  ~ ANY)* ~ "\"" }
float = @{ ("-")? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
integer = @{ ("-")? ~ ASCII_DIGIT+ }
boolean = @{ "true" | "false" }
member_access = { "." ~ identifier }
member_call = { "." ~ function_call }

eq_ops = { "==" | "!="  | ">=" | "<=" | ">" | "<"}
sum_ops = { "+" | "-" }
prod_ops = { "*" | "/" }

expr = {
    bare_expr |
    function_call
}
function_call = { identifier ~ "(" ~ expr* ~ ("," ~ expr)* ~ ")" }
bare_expr = { sum ~ ((eq_ops) ~ sum)* }
sum = { product ~ ((sum_ops) ~ product)* }
product = { term ~ ((prod_ops) ~ term)* }
term = {
    (string |
    float |
    integer |
    boolean |
    function_call |
    identifier |
    "(" ~ expr ~ ")" ) ~ (member_access | member_call)*
}

class_declaration = {"klase" ~ identifier ~ "{" ~ natural_newline* ~ identifier? ~ ("," ~ natural_newline* ~ identifier ~ natural_newline*)* ~ ","? ~ natural_newline* ~ "}"}

variable_assignment = {"deklara" ~ identifier ~ "=" ~ expr}
variable_reassignment = { identifier ~ "=" ~ expr}
function_call_statement = { function_call }
function_declaration = {"proseso" ~ identifier ~ "(" ~ identifier? ~ ("," ~ identifier)* ~ ","? ~ ")" ~ block}
conditional_if = {"kon" ~ expr ~ block }
conditional_else_if = {"kon" ~ "lain" ~ expr ~ block }
conditional_else = {"lain" ~ block }
conditional = { conditional_if ~ (conditional_else_if)* ~ (conditional_else)? }
while_loop = {"samtang" ~ expr ~ block }
break_kw = {"buwag"}
continue_kw = {"tiwas"}
return_kw = {"balik" ~ expr* ~ ("," ~ expr*)* ~ ","*}
comment = { "//" ~ (!natural_newline ~ ANY)* }


block = { "{" ~ inner_statements ~ "}"}

inner_statement = _{
    (
        variable_assignment
        | function_call_statement
        | conditional
        | variable_reassignment
        | while_loop
        | break_kw
        | continue_kw
        | return_kw
        | comment
    )
    ~ new_line
}

inner_statements = _{ new_line* ~ inner_statement* ~ new_line*}

outer_statement = _{
    (new_line* ~ (
        inner_statement
        | function_declaration
        | class_declaration
        | comment
    ) ~ new_line*)
}


program = _{ SOI ~ outer_statement* ~ EOI}

WHITESPACE = _{ " " | "\t" }